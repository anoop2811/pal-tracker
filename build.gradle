import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    ext {
        kotlinVersion = '1.2.41'
        springBootVersion = '1.5.13.RELEASE'
        flywayVersion = '5.0.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.flywaydb:flyway-gradle-plugin:${flywayVersion}")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.flywaydb.flyway'

group = 'io.pivotal.pal.tracker'
sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
}


dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile('com.fasterxml.jackson.core:jackson-annotations')
    compile('com.fasterxml.jackson.core:jackson-core')
    compile('com.fasterxml.jackson.module:jackson-module-kotlin')
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.5")
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("org.jetbrains.kotlin:kotlin-reflect")
    compile("org.springframework.boot:spring-boot-starter-jdbc")
    compile("mysql:mysql-connector-java:6.0.6")

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')
}

def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"

bootRun.environment([
        "WELCOME_MESSAGE"      : "hello",
        "SPRING_DATASOURCE_URL": developmentDbUrl,
        "MANAGEMENT_SECURITY_ENABLED": false,
])

test.environment([
        "WELCOME_MESSAGE"      : "Hello from test",
        "SPRING_DATASOURCE_URL": testDbUrl,
        "MANAGEMENT_SECURITY_ENABLED": false,
])

flyway {
    url = developmentDbUrl
    user = "tracker"
    password = ""
    locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
    url = testDbUrl
}

springBoot {
    buildInfo()
}